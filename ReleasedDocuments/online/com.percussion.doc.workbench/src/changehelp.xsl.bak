<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:variable name="titleDoc" select="document('Workbench Online Help 6_0.hs')"/>
	<xsl:variable name="helpMapDoc" select="document('map.jhm')"/>
	<!-- main template -->
	<xsl:template match="/">
		<toc>
			<xsl:attribute name="label"><xsl:value-of select="$titleDoc//title"/></xsl:attribute>
			<xsl:apply-templates select="*/tocitem" mode="copy"/>
		</toc>
	</xsl:template>
	<!-- copy any attribute or template -->
	<xsl:template match="@*|*" mode="copy">
		<xsl:copy>
			<xsl:apply-templates select="@*" mode="copy"/>
			<xsl:apply-templates mode="copy"/>
		</xsl:copy>
	</xsl:template>
	<!--Template matches on and copies all elements-->
	<xsl:template match="tocitem" mode="copy">
		<xsl:variable name="toctarget" select="@target"/>
		<topic>
			<xsl:attribute name="label"><xsl:value-of select="@text"/></xsl:attribute>
			<xsl:attribute name="href"><xsl:value-of select="$helpMapDoc/map/mapID[@target=$toctarget]/@url"/></xsl:attribute>
			<xsl:apply-templates mode="copy"/>
		</topic>
		<!-- Add API JavaDoc topic after 'About the Rx Workbench' topic -->
		<xsl:if test="@text = 'About the Rhythmyx Workbench'">
		   <topic label="Rhythmyx API JavaDoc" href="JavaDocs/index.html"/>
		</xsl:if>
	</xsl:template>
</xsl:stylesheet>
